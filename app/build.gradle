apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "diefferson.androidapp"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        //Nome da engine que vamos usar
        buildConfigField "String", "FLUTTER_ENGINE", '"diefferson.androidapp.FlutterEngine"'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //arquiteturas suportadas pelo flutter
        ndk {
            // Filter for architectures supported by Flutter.
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "environment"
    productFlavors {
        dev {
            dimension "environment"
            //método main do Flutter para o flavor Dev
            resValue "string", "flutter_entry", "mainDev"
        }
        sandbox {
            dimension "environment"
            //método main do Flutter para o flavor Sandbox
            resValue "string", "flutter_entry", "mainSandbox"
        }
        prod {
            dimension "environment"
            //método main do Flutter para o flavor Prod
            resValue "string", "flutter_entry", "mainProd"
        }
    }

    //O Flutter precisa da compatilidade com Java 8
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    //Implementation do modulo flutter
    implementation project(':flutter')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}
